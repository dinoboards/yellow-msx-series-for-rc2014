Name     memory ;
PartNo   00 ;
Date     15/12/2020 ;
Revision 01 ;
Designer Dean Netherton ;
Company  Dino ;
Assembly None ;
Location  ;
Device   g22v10;

/************* INPUT PINS *********************/
PIN  1 = SLTLOW;
PIN  2 = SLTHIGH;
PIN  3 = MREQ;
PIN  4 = FCFF;
PIN  5 = SUBSLTL;
PIN  6 = SUBSLTH;
PIN  7 = A0;
PIN  8 = A1;
PIN  9 = FFXX;
PIN 10 = RD;
PIN 11 = WR;
PIN 12 = GND;
PIN 13 = IORQ;

/**************** OUTPUT PINS *********************/
PIN 14 = !SLTSL1;
PIN 15 = !SLTSL2;
PIN 16 = !SLTSL31;
PIN 17 = !RAMCS;
PIN 18 = !ROMCS;
PIN 19 = !PAGEWR;
PIN 20 = ROA16;
PIN 21 = !SLTWR;
PIN 22 = !SLTRD;
PIN 23 = !SLTSL33;

SLTSL1 = !SLTHIGH & !SLTLOW & !MREQ;
SLTSL2 = SLTHIGH & !SLTLOW & !MREQ;
SLTSL31	= SLTHIGH & SLTLOW & !SUBSLTH & SUBSLTL & !MREQ;	// SLOT 3-1 (EXTERNAL EXPANSION ROM)
SLTSL33	= SLTHIGH & SLTLOW & SUBSLTH & SUBSLTL & !MREQ;		// SLOT 3-3 (INTERNAL EXPANSION ROM)
RAMCS = SLTHIGH & SLTLOW & SUBSLTH & !SUBSLTL & !MREQ;		// SLOT 3, SUB SLOT 2 AND MREQ

ROMCS		= !SLTHIGH & !SLTLOW & !MREQ #						/* SLOT 0 AND MREQ */
		  SLTHIGH & SLTLOW & !MREQ & !A0 & !SUBSLTH & !SUBSLTL #		/* OR SLOT 3, SUB SLOT 0, MREQ AND NOT FFFF */
		  SLTHIGH & SLTLOW & !MREQ & !A1 & !SUBSLTH & !SUBSLTL #		/* OR SLOT 3, SUB SLOT 0, MREQ AND NOT FFFF */
		  SLTHIGH & SLTLOW & !MREQ & FCFF & !SUBSLTH & !SUBSLTL #		/* OR SLOT 3, SUB SLOT 0, MREQ AND NOT FFFF */
		  SLTHIGH & SLTLOW & !MREQ & FFXX & !SUBSLTH & !SUBSLTL	#		/* OR SLOT 3, SUB SLOT 0, MREQ AND NOT FFFF */
		  SLTHIGH & SLTLOW & !MREQ & !A0 & SUBSLTH & SUBSLTL #			/* OR SLOT 3, SUB SLOT 3, MREQ AND NOT FFFF */
		  SLTHIGH & SLTLOW & !MREQ & !A1 & SUBSLTH & SUBSLTL #			/* OR SLOT 3, SUB SLOT 3, MREQ AND NOT FFFF */
		  SLTHIGH & SLTLOW & !MREQ & FCFF & SUBSLTH & SUBSLTL #			/* OR SLOT 3, SUB SLOT 3, MREQ AND NOT FFFF */
		  SLTHIGH & SLTLOW & !MREQ & FFXX & SUBSLTH & SUBSLTL;			/* OR SLOT 3, SUB SLOT 3, MREQ AND NOT FFFF */

ROA16		= SLTHIGH & SLTLOW & !SUBSLTH & !SUBSLTL;				// SET A16 TO 1 WHEN SLOT 3-0, OTHERWISE ITS 0
ROA16.OE	= !SLTSL33;								// NOT SLOT 3-3

SLTWR = !FCFF & !FFXX & A0 & A1 & SLTHIGH & SLTLOW & !MREQ & !WR;			// FFFF AND SLOT 3 AND MREQ AND WR
SLTRD = !FCFF & !FFXX & A0 & A1 & SLTHIGH & SLTLOW & !MREQ & !RD;			// FFFF AND SLOT 3 AND MREQ AND RD

PAGEWR = !FCFF & !WR & !IORQ;
