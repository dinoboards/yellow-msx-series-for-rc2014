
	INCLUDE	"msx.inc"
	INCLUDE	"../sio.inc"

; SIO CHIP PROBE
; CHECK FOR PRESENCE OF SIO CHIPS AND POPULATE THE
; SIO_MAP BITMAP (ONE BIT PER CHIP).  THIS DETECTS
; CHIPS, NOT CHANNELS.  EACH CHIP HAS 2 CHANNELS.
; MAX OF TWO CHIPS CURRENTLY.  INT VEC VALUE IS TRASHED!
;
	SECTION	CODE
	PUBLIC	_sio_probe
	EXTERN	RS_SIO_CLK_DIV, RS_IQLN, RS_FCB
	EXTERN	RS_DATCNT, RS_FLAGS

_sio_probe:
	XOR	A
	LD	B, 2			; WR2 REGISTER (INT VEC)
	LD	C, RC_SIOB_CMD		; FIRST CHIP
	CALL	SIO_WR			; WRITE ZERO TO CHIP REG

	; CHECK FOR STD RC2014 PORT MAPPINGS
	LD	C, RC_SIOB_CMD		; FIRST CHIP CMD/STAT PORT
	CALL	SIO_PROBECHIP		; PROBE IT
	JR	NZ, SIO_PROBE_NOT_FOUND	; NOT FOUND, TRY OTHER MAPPING

	; LD	A, SIO_CLK_DIV_16	; DEFAULT TO DIV 16
	; LD	(RS_SIO_CLK_DIV), A

	LD	L, 1			; RETURN PORT SET 1
	RET

SIO_PROBE_NOT_FOUND:
	LD	L, 0
	RET
;
SIO_PROBECHIP:
	; READ WR2 TO ENSURE IT IS ZERO (AVOID PHANTOM PORTS)
	CALL	SIO_RD			; GET VALUE
	AND	$F0			; ONLY TOP NIBBLE
	RET	NZ			; ABORT IF NOT ZERO
	; WRITE INT VEC VALUE TO WR2
	LD	A,$FF			; TEST VALUE
	CALL	SIO_WR			; WRITE IT
	; READ WR2 TO CONFIRM VALUE WRITTEN
	CALL	SIO_RD			; REREAD VALUE
	AND	$F0			; ONLY TOP NIBBLE
	CP	$F0			; COMPARE
	RET

SIO_WR:
	OUT	(C),B			; SELECT CHIP REGISTER
	OUT	(C),A			; WRITE VALUE
	RET
;
SIO_RD:
	OUT	(C),B			; SELECT CHIP REGISTER
	IN	A,(C)			; GET VALUE
	RET
