


EXTBIO_RS232_ID			EQU	8
EXTBIO_RS2_GET_INFO_TABLE_FN	EQU	0

EXTBIO:
	PUSH	AF
	LD	A, D
	OR	E
	JR	Z, EXTBIO_GET_DEVICE_ID

	LD	A, D
	CP	EXTBIO_RS232_ID
	JR	NZ, EXBIO_EXIT			; NOT US

	LD	A, E
	CP	EXTBIO_RS2_GET_INFO_TABLE_FN
	JR	Z, EXTBIO_RS2_GET_INFO_TABLE

	JR	EXBIO_EXIT


EXTBIO_GET_DEVICE_ID:
	PUSH	DE
	PUSH	BC
	LD	E, EXTBIO_RS232_ID
	LD	A, B
	CALL	WRSLT
	INC	HL
	LD	E, 0		; reserved byte
	LD	A, B
	CALL	WRSLT
	INC	HL
	POP	BC
	POP	DE

EXBIO_EXIT:
	POP	AF
	JP	MEXBIH


EXTBIO_RS2_GET_INFO_TABLE:
	PUSH	DE
	PUSH	BC
	CALL	GETSL10

	LD	E, A
	LD	A, B
	CALL	WRSLT
	POP	BC
	PUSH	BC
	INC	HL

	LD	E, EXTBIO_RS2_JUMP_TABLE & 255
	LD	A, B
	CALL	WRSLT
	POP	BC
	PUSH	BC
	INC	HL

	LD	E, EXTBIO_RS2_JUMP_TABLE / 256
	LD	A, B
	CALL	WRSLT
	POP	BC
	INC	HL

	POP	DE
	POP	AF
	JP	MEXBIH
