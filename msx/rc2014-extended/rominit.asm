
; allows for installation of expansion devices that contain extra OS subroutines

ROM_INIT:
	CALL	PROBE_HARDWARE

	LD	A, (RS_HOKVLD)
	BIT	0, A
	JR	NZ, initialise_serial_state

	; THERE IS NO EXTBIO INSTALL - SO WE HAVE TO INITIALISE
	LD	A, 1
	LD	(RS_HOKVLD), A
	LD	A, $C9
	LD	(RS_MEXBIH), A		; SET OPERATION CODE OF 'RET'
	JR	INST_HOOK

initialise_serial_state:
	XOR	A
	LD	(RS_FLAGS), A		; ENSURE RS232 IS MARKED AS CLOSED WITH RTS_OFF

	IF BUS_CLK = 307200
	LD	A, SIO_CLK_307200

	ELSEIF BUS_CLK = 614400
	LD	A, SIO_CLK_614400

	ELSE
	ASSERT	0, "UNKNOWN BUS_CLK VALUE"
	ENDIF

	LD	(RS_SIO_CLK), A

	LD	DE, RS_MEXBIH		; COPY EXTBIO HOOK FUNCTION FROM H_BEXT TO RS_MEXBIH
	LD	HL, H_BEXT
	LD	BC, 5
	LDIR

INST_HOOK:
	DI

	LD	A, $F7			; 'RST 30H' INTER-SLOT CALL OPERATION CODE
	LD	(H_BEXT), A		; SET NEW HOOK OP-CODE

	CALL	GETSL10			; GET OUR SLOT ID (EXPECT 3-3 $F7)
	LD	(H_BEXT+1), A		; SET SLOT ADDRESS

	LD	HL, EXTBIO		; GET OUR INTERRUPT ENTRY POINT
	LD	(H_BEXT+2), HL		; SET NEW INTERRUPT ENTRY POINT

	LD	A, $C9			; 'RET' OPERATION CODE
	LD	(H_BEXT+4), A		; SET OPERATION CODE OF 'RET'
	EI

	LD	HL, FOSSIL_DRV_LENGTH	; ALLOC MEMORY FOR FOSSIL JUMP TABLE
	CALL	ALLOC			; THIS MAY NOT WORK IF IT HAPENS BEFORE DISK ROM
	LD	(WORK), HL
	JR	C, ALLOC_FAILED

	; FOR PERFORMANCE REASONS, WE KNOW WE ARE SLOT 3-3 - SO JUST HARD CODE THE SLTWRK OFFSET
	; CALL	WSLW10

	LD	DE, @FOSSIL_DRV_START
	EX	DE, HL
	LD	BC, @FOSSIL_DRV_LENGTH
	LDIR

	; relocate the fossil driver
	LD	IX, @FOSSILE_DRV_MAP
	LD	BC, @FOSSILE_DRV_MAP_LENGTH
	LD	DE, (WORK)		; DE' => amount required to be added to

LOOP:	LD	L, (IX)
	INC	IX
	LD	H, (IX)
	INC	IX
	ADD	HL, DE			; HL -> ADDRESS REFERENCE TO BE RELOCATED

	PUSH	HL			; SAVE ADDRESS REFERENCE ADDRESS
	LD	A, (HL)
	INC	HL
	LD	H, (HL)
	LD	L, A			; ADDRESS VALUE TO BE RELOCATED

	ADD	HL, DE			; RELOCATE ADDR

	POP	IY			; GET ADDR REFERENCE
	LD	(IY), L
	LD	(IY+1), H		; RELOCATE THE ADDRESS

	DEC	BC
	LD	A, B
	OR	C
	JR	NZ, LOOP

	RET

ALLOC_FAILED:
	LD	DE, MSG.ALLOC_FAILED
	CALL	PRINT
	RET

MSG.ALLOC_FAILED:
	DB	"RC2014 Driver: Insufficient page 3 memory available", 13, 10, 0
