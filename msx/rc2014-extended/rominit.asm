
; allows for installation of expansion devices that contain extra OS subroutines

ROM_INIT:
	CALL	PROBE_HARDWARE

	LD	A, (HOKVLD)
	BIT	0, A
	JR	NZ, INJECT_HOOK_HANDLERS

	; THERE IS NO EXTBIO INSTALL - SO WE HAVE TO INITIALISE
	LD	A, 1
	LD	(HOKVLD), A
	LD	A, $C9
	LD	(MEXBIH+0), A		; SET OPERATION CODE OF 'RET'
	JR	INST_HOOK

INJECT_HOOK_HANDLERS:
	LD	DE, MEXBIH		; COPY EXTBIO HOOK FUNCTION FROM H_BEXT TO MEXBIH
	LD	HL, H_BEXT
	LD	BC, 5
	LDIR

INST_HOOK:
	LD	DE, OLDINT		; COPY HOOK FUNCTION FROM H_KEYI TO OLDINT
	LD	HL, H_KEYI
	LD	BC, 5
	LDIR

	DI

	LD	A, $F7			; 'RST 30H' INTER-SLOT CALL OPERATION CODE
	LD	(H_BEXT), A		; SET NEW HOOK OP-CODE
	LD	(H_KEYI), A		; SET NEW HOOK OP-CODE

	CALL	GETSL10			; GET OUR SLOT ID (EXPECT 3-3)
	LD	(H_BEXT+1), A		; SET SLOT ADDRESS
	LD	(H_KEYI+1), A		; SET SLOT ADDRESS

	LD	HL, EXTBIO		; GET OUR INTERRUPT ENTRY POINT
	LD	(H_BEXT+2), HL		; SET NEW INTERRUPT ENTRY POINT

	LD	HL, NEW_KEYI		; GET OUR INTERRUPT ENTRY POINT
	LD	(H_KEYI+2), HL		; SET NEW INTERRUPT ENTRY POINT

	LD	A, $C9			; 'RET' OPERATION CODE
	LD	(H_BEXT+4), A		; SET OPERATION CODE OF 'RET'
	LD	(H_KEYI+4), A		; SET OPERATION CODE OF 'RET'

	EI
	; allocate memory for buffer etc.
	; implement driver

	RET
