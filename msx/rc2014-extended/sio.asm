




SIO_INIT:	macro	CMD, RTS
	; WR0: CHANNEL RESET CMD
	LD	A, 0
	OUT	(CMD), A
	LD	A, 018H
	OUT	(CMD), A

	; WR4: CLK BAUD PARITY STOP BIT
	LD	A, 4
	OUT	(CMD), A
	LD	A, 0C4H
	OUT	(CMD), A

	; WR1: INTERRUPT ON
	LD	A, 1
	OUT	(CMD), A
	LD	A, SIO_WR1VAL
	OUT	(CMD), A

	; WR2: IM2 VEC OFFSET
	LD	A, 2
	OUT	(CMD), A
	LD	A, 00H
	OUT	(CMD),A

	; WR3: 8 BIT RCV, CTS/DCD AUTO, RX ENABLE
	LD	A, 3
	OUT	(CMD), A
	LD	A, 0E1H
	OUT	(CMD), A

	; WR5: DTR, 8
	LD	A, 5
	OUT	(CMD), A
	LD	A, RTS
	OUT	(CMD), A
	ENDM

SIO_OUT_CHR:	macro	chr
	LD 	A, chr		; REQUEST NEXT PACKET
	OUT	(DAT_CH), A	; send to port
	endm

SIO_OUT_A:	macro
	OUT	(DAT_CH), A	; send to port
	endm

; SIO CHIP PROBE
; CHECK FOR PRESENCE OF SIO CHIPS AND POPULATE THE
; SIO_MAP BITMAP (ONE BIT PER CHIP).  THIS DETECTS
; CHIPS, NOT CHANNELS.  EACH CHIP HAS 2 CHANNELS.
; MAX OF TWO CHIPS CURRENTLY.  INT VEC VALUE IS TRASHED!
;
SIO_PROBE:
	; INIT THE INT VEC REGISTER OF ALL POSSIBLE CHIPS
	; TO ZERO.
	XOR	A
	LD	B, 2			; WR2 REGISTER (INT VEC)
	LD	C, SIO0B_CMD		; FIRST CHIP
	CALL	SIO_WR			; WRITE ZERO TO CHIP REG

	; FIRST POSSIBLE CHIP
	LD	C, SIO0B_CMD		; FIRST CHIP CMD/STAT PORT
	CALL	SIO_PROBECHIP		; PROBE IT
	RET
;
SIO_PROBECHIP:
	; READ WR2 TO ENSURE IT IS ZERO (AVOID PHANTOM PORTS)
	CALL	SIO_RD			; GET VALUE
	AND	$F0			; ONLY TOP NIBBLE
	RET	NZ			; ABORT IF NOT ZERO
	; WRITE INT VEC VALUE TO WR2
	LD	A,$FF			; TEST VALUE
	CALL	SIO_WR			; WRITE IT
	; READ WR2 TO CONFIRM VALUE WRITTEN
	CALL	SIO_RD			; REREAD VALUE
	AND	$F0			; ONLY TOP NIBBLE
	CP	$F0			; COMPARE
	RET

SIO_WR:
	OUT	(C),B			; SELECT CHIP REGISTER
	OUT	(C),A			; WRITE VALUE
	RET
;
SIO_RD:
	OUT	(C),B			; SELECT CHIP REGISTER
	IN	A,(C)			; GET VALUE
	RET

SIO_INIT_CHANNELS:
	DI
	SIO_INIT	SIO0A_CMD, SIO_RTSOFF
	SIO_INIT	SIO0B_CMD, SIO_RTSON
	EI
	RET


SIO_RCBBYT:
	LD	A, (RSIQLN)
	INC	A
	INC	A
	LD	B, A

	LD	HL, (RSFCB)
	DI				; AVOID COLLISION WITH INT HANDLER
	LD	A, (DATCNT)		; GET COUNT
	DEC	A			; DECREMENT COUNT
	LD	(DATCNT), A		; SAVE UPDATED COUNT
	CP	0			; BUFFER LOW THRESHOLD
	JR	NZ, SIO_IN1		; IF NOT, BYPASS SETTING RTS

	LD	A, 5			; RTS IS IN WR5
	OUT	(CMD_CH), A		; ADDRESS WR5
	LD	A, SIO_RTSON		; VALUE TO SET RTS
	OUT	(CMD_CH), A		; DO IT

	LD	A, 255
	LD	(SIO_RTS), A
SIO_IN1:
	INC	HL
	INC	HL			; HL NOW HAS ADR OF TAIL PTR
	PUSH	HL			; SAVE ADR OF TAIL PTR
	LD	A, (HL)			; DEREFERENCE HL
	INC	HL
	LD	H, (HL)
	LD	L, A			; HL IS NOW ACTUAL TAIL PTR
	LD	C, (HL)			; C := CHAR TO BE RETURNED
	INC	HL			; BUMP TAIL PTR
	POP	DE			; RECOVER ADR OF TAIL PTR
	LD	A, L			; GET LOW BYTE OF TAIL PTR
	SUB	B			; SUBTRACT SIZE OF BUFFER AND POINTER
	CP	E			; IF EQUAL TO START, TAIL PTR IS PAST BUF END
	JR	NZ, SIO_IN2		; IF NOT, BYPASS
	LD	H, D			; SET HL TO
	LD	L, E			; ... TAIL PTR ADR
	INC	HL			; BUMP PAST TAIL PTR
	INC	HL			; ... SO HL NOW HAS ADR OF ACTUAL BUFFER START
SIO_IN2:
	EX	DE, HL			; DE := TAIL PTR VAL, HL := ADR OF TAIL PTR
	LD	(HL), E			; SAVE UPDATED TAIL PTR
	INC	HL
	LD	(HL), D
	EI				; INTERRUPTS OK AGAIN
	LD	A, C			; MOVE CHAR TO RETURN TO A
	OR	A
	RET
