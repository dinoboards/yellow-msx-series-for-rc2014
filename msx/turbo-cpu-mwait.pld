Name     turbo-cpu-mwait ;
PartNo   00 ;
Date     15/12/2020 ;
Revision 01 ;
Designer Dean Netherton ;
Company  Dino ;
Assembly None ;
Location Ignored ;
Device   g16v8a ;


/************* INPUT PINS *********************/
PIN  1 = CPU_CLK;
PIN  2 = !M1;
PIN  3 = !MREQ;
PIN  4 = !BWAIT;
PIN  5 = !MODE_FAST;
PIN  6 = !RFSH;
PIN  7 = !IORQ;
PIN  8 = !MODE_NORMAL;
PIN  9 = !NOT_MODE_NORMAL;   // INVERT OF MODE_NORMAL - TODO: REMOVE THIS PIN
PIN 10 = GND;
//PIN 11 = ;

/**************** OUTPUT PINS *********************/
PIN 12 = T1;
PIN 13 = T2;
PIN 14 = T3;
PIN 15 = !MWAIT1;
PIN 16 = !CWAIT;
PIN 17 = !MWAIT2;
PIN 18 = T4;
PIN 19 = TFIN;
PIN 20 = VCC;

MODE_MIDDLE=!MODE_NORMAL & !MODE_FAST;

MR = (MODE_NORMAL & MREQ & !RFSH & M1) # (MODE_MIDDLE & MREQ & !RFSH) # (MODE_FAST & MREQ & !RFSH);

MWAIT1.D = (!MWAIT1 & MR) & !MWAIT2;
MWAIT2.D = (MR & MWAIT2) # (MWAIT1);

WAIT_ENABLE=(MWAIT1 # MWAIT2);
T0 = !T1 & !T2 & !T3 & !T4 & !TFIN;

CWAIT = BWAIT # (T0 & MR) # (WAIT_ENABLE & (T0 # !TFIN));

T1.D =  MR & !T1 & !T2 & !T3 & !T4 & !TFIN;
T2.D =  MR &  T1 & !T2 & !T3 & !T4 & !TFIN;
T3.D =  MR & !T1 &  T2 & !T3 & !T4 & !TFIN;
T4.D =  MR & !T1 & !T2 &  T3 & !T4 & !TFIN;

TFIN.D = WAIT_ENABLE & (TFIN # (MODE_MIDDLE & T3) # (!MODE_MIDDLE & T1));
