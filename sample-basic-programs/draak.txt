100 REM DRAAK - Dragon
110 REM
120 REM MSX Computer Magazine #19, 1987
130 REM
140 DIM H1(2), H2(2), IS(12), SS(12), L(12), ALPHA(12)
150 MLIJN=2:' MLINE
160 FOR I=1 TO MLIJN
170   READ H1(I),S$
180   IF S$="swap" THEN S1$=S1$+CHR$(I)
190   READ H2(I),S$
200   IF S$="swap" THEN S2$=S2$+CHR$(I)
210 NEXT I
220 DATA -45, noswap, +45, swap
230 DATA +90, swap,   -90, noswap
240 '
250 INPUT "give sign depth 1-12";SM
260 IF SM<1 OR SM>12 THEN 250
270 WX=60: WY=120
280 S=0: L(S)=120: ALPHA(S)=0
290 SCREEN 2
300 GOSUB 350
310 BEEP
320 GOTO 320
330 END
340 '
350 ' the recursive dragon routine
360 '
370 S=S+1
380 L(S)=L(S-1)/SQR(2)
390 ALPHA(S)=ALPHA(S-1)
400 FOR I=1 TO MLIJN
410   ALPHA(S)=ALPHA(S)+H1(I)
420   IF S>=SM THEN GOSUB 640: GOTO 500
430  'ELSE
440   SS(S)=INSTR(S1$,CHR$(I)) 'verwissel flag
450   IF SS(S) THEN GOSUB 540  'swap h1 met h2
460   IS(S)=I
470   GOSUB 350
480   I=IS(S)
490   IF SS(S) THEN GOSUB 540  'swap h2 with h1
500   NEXT I
510 S=S-1
520 RETURN
530 '
540 ' swap h1 - h2
550 '
560 FOR J=1 TO MLIJN
570   SWAP H1(J),H2(J)
580 NEXT J
590 SWAP S1$,S2$
600 RETURN
610 '
620 ' draw line
630 '
640 RA=ALPHA(S)/57.295779513082#
650 NX=L(S)*COS(RA)+WX
660 NY=L(S)*SIN(RA)*1.3+WY
670 LINE (WX,190-WY)-(NX,190-NY),3
680 WX=NX: WY=NY
690 RETURN
