
	include "cbios/src/hardware.asm"
	include "cbios/src/hooks.asm"

RG1SAV:		EQU	$F3E0
EXPTBL:		EQU	$FCC1		; SLOTS EXPANDED OR NOT
PSLTRG:		EQU	$A8		; I/O PORT ADDRESS OF PRIMARY SLOT REGISTER
VDP_INTEN:	EQU	5

INSTALL_INTERRUPT_HANDLER	MACRO
	DI

	LD	DE, ORG_H_KEYI		; GET ADDRESS OF OLD INT. HOOK SAVED AREA
	LD	HL, H_KEYI		; GET ADDRESS OF INTERRUPT ENTRY HOOK
	LD	BC, 5			; LENGHT OF HOOK IS 5 BYTES
	LDIR				; TRANSFER

	; GET CURRENT SLOT NUMBER
	IN	A, (PSLTRG)		; READ PRIMARY SLOT REGISTER
	RRCA				; MOVE IT TO BIT 0,1 OF A
	RRCA
	AND	00000011B		; GET BIT 1,0
	LD	C, A			; SET PRIMARY SLOT NO.
	LD	B, 0
	LD	HL, EXPTBL		; SEE IF THE SLOT IS EXPANDED OR NOT
	ADD	HL, BC
	OR	(HL)			; SET MSB IF SO
	LD	C, A
	INC	HL			; POINT TO SLTTBL ENTRY
	INC	HL
	INC	HL
	INC	HL
	LD	A, (HL)			; GET WHAT IS CURRENTLY OUTPUT TO
	; EXPANSION SLOT REGISTER

	AND	00001100B		; GET BITS 3,2
	OR	C			; FINALLY FORM SLOT ADDRESS

	LD	(H_KEYI+1), A		; SET SLOT ADDRESS
	LD	A, $F7			; 'RST 30H' INTER-SLOT CALL OPERATION CODE
	LD	(H_KEYI), A		; SET NEW HOOK OP-CODE
	LD	HL,SIO_INT		; GET OUR INTERRUPT ENTRY POINT
	LD	(H_KEYI+2), HL		; SET NEW INTERRUPT ENTRY POINT
	LD	A, $C9			; 'RET' OPERATION CODE
	LD	(H_KEYI+4), A		; SET OPERATION CODE OF 'RET'
	EI
	ENDM

REMOVE_INTERRUPT_HANDLER	MACRO
	LD	DE, H_KEYI
	LD	HL, ORG_H_KEYI
	LD	BC, 5
	LDIR
	ENDM

DISABLE_VDP_INTERRUPTS		MACRO
	DI
	LD	A, (RG1SAV)
	RES	VDP_INTEN, A		; RESET INTERRUPT ENABLE BIT
	LD	(RG1SAV), A
	OUT	(VDP_ADDR), A
	NOP
	NOP
	LD	A, $81			; SELECT REGISTER 1
	OUT	(VDP_ADDR), A
	EI
	ENDM

ENABLE_VDP_INTERRUPTS		MACRO
	DI
	LD	A, (RG1SAV)
	SET	VDP_INTEN, A		; RESET INTERRUPT ENABLE BIT
	LD	(RG1SAV), A
	OUT	(VDP_ADDR), A
	NOP
	NOP
	LD	A, $81			; SELECT REGISTER 1
	OUT	(VDP_ADDR), A
	EI
	ENDM
